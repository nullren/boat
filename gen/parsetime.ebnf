Parsetime = SpecBase | SpecBase IncOrDec .

SpecBase = Date | Time | (Time Date) | now .
now = "now" .

Time = TimeBase | (TimeBase TimezoneName) .
TimeBase = Hr24clockHrMin
    | (TimeHour AmPm)
    | TimeHourMin
    | (TimeHourMin AmPm)
    | noon
    | midnight
    | teatime .

noon = "noon" .
midnight = "midnight" .
teatime = "teatime" .

Hr24clockHrMin = Int4digit .
TimeHour = Int1_2digit .
TimeHourMin = Int1_2digit ":" Int2digit .

AmPm = am | pm .
am = "am" .
pm = "pm" .

TimezoneName = utc .
utc = "Z" | "z" | "gmt" | "GMT" .

Date = (MonthName DayNumber)
    | (MonthName DayNumber YearNumber)
    | (MonthName DayNumber "," YearNumber)
    | DayOfWeek
    | today
    | tomorrow
    | (DayNumber MonthName)
    | (DayNumber MonthName YearNumber)
    | (MonthNumber "/" DayNumber "/" YearNumber)
    | (MonthNumber "-" DayNumber "-" YearNumber)
    | (MonthNumber "." DayNumber "." YearNumber)
    | ConcatenatedDate
    | (next IncDecPeriod)
    | (next DayOfWeek) .

today = "today" | "tdy" .
tomorrow = "tomorrow" | "tmrw" .
next = "next" .

ConcatenatedDate = Int5_8digit .

MonthName = jan | feb | mar | apr | may | jun | jul | aug | sep | oct | nov | dec .
jan = "january" | "jan" .
feb = "february" | "feb" .
mar = "march" | "mar" .
apr = "april" | "apr" .
may = "may" .
jun = "june" | "jun" .
jul = "july" | "jul" .
aug = "august" | "aug" .
sep = "september" | "sep" .
oct = "october" | "oct" .
nov = "november" | "nov" .
dec = "december" | "dec" .

MonthNumber = Int1_2digit .
DayNumber = Int1_2digit .
YearNumber = Int2_or_4digit .

DayOfWeek = sun | mon | tue | wed | thu | fri | sat .
sat = "saturday" | "sat" .
fri = "friday" | "fri" .
thu = "thursday" | "thu" .
wed = "wednesday" | "wed" .
tue = "tuesday" | "tue" .
mon = "monday" | "mon" .
sun = "sunday" | "sun" .

IncOrDec = Increment | Decrement .
Increment = "+" IncDecNumber IncDecPeriod .
Decrement = "-"  IncDecNumber  IncDecPeriod .
IncDecNumber = Integer .

IncDecPeriod = minute | hour | day | week | month | year .
year = "y" | "year" | "years" .
month = "m" | "month" | "months" .
week = "w" | "week" | "weeks" .
day = "d" | "day" | "days" .
hour = "h" | "hr" | "hour" | "hours" | "hrs" .
minute = "m" | "min" | "minute" | "minutes" | "mins" .

Int1_2digit = int1digit | Int2digit .
Int2_or_4digit	= Int2digit | Int4digit .
Integer = Int | int1digit | Int2digit | Int4digit | Int5_8digit .

Int = { int1digit } .
Int5_8digit = Int4digit (int1digit | Int2digit | Int3digit | Int4digit) .
Int4digit = Int2digit Int2digit .
Int3digit = int1digit Int2digit .
Int2digit = int1digit int1digit .
int1digit = "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" .
